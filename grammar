<база знаний> ::= <заголовок><таблица следствия><новая строка><таблица правила>|<заголовок><таблица правила><новая строка><таблица следствия>
<заголовок> ::= <имя предиката посылки><новая строка><новая строка>
<имя предиката посылки> ::= <имя>
<таблица следствия> ::= <элементы второго домена><новая строка><степень истинности аргументов следствия><новая строка>
<элементы второго домена> ::= <элемент>|<элемент><разделители><элементы второго домена>
<степень истинности аргументов следствия> ::= <степень принадлежности>|<степень принадлежности><разделители><степень истинности аргументов следствия>
<таблица правила> ::= <элементы первого домена><новая строка><строки таблицы правила><новая строка>
<элементы первого домена> ::= <элемент>|<элемент><разделители><элементы первого домена>
<строки таблицы правила> ::= <строка таблицы правила>|<строка таблицы правила><новая строка><строки таблицы правила>
<строка таблицы правила> ::= <степень принадлежности>|<степень принадлежности><разделители><строка таблицы правила>
<элемент> ::= <имя>
<имя> ::= <буква>|<буква><символы>
<символы> ::= <символ>|<символ><символы>
<степень принадлежности> ::= <действительное число с 0 до 1>
<действительное число с 0 до 1> ::= <единица>|<единица><точка><нули>|<ноль>|<ноль><точка><цифры>
<нули> ::= <ноль>|<ноль><нули>
<цифры> ::= <цифра>|<цифра><цифры>
<разделители> ::= <пробел>|<пробел><разделители>
<символ> ::= <буква>|<цифра>
<буква> ::= A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|x|y|z
<цифра> ::= 0|1|2|3|4|5|6|7|8|9
<единица> ::= 1
<ноль> ::= 0
<точка> ::= .

